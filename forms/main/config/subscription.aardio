import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text="aardio form";right=892;bottom=564;bgcolor=16777215)
winform.add(
btnAdd={cls="plus";text="添加订阅";left=10;top=4;right=143;bottom=31;align="left";dl=1;dt=1;font=LOGFONT(h=-15);iconStyle={align="left";font=LOGFONT(h=-13;name='FontAwesome');padding={left=12}};iconText='\uF196 ';notify=1;textPadding={left=30};z=5};
btnRemove={cls="plus";text="移除选中订阅";left=143;top=4;right=290;bottom=31;align="left";dl=1;dt=1;font=LOGFONT(h=-15);iconStyle={align="left";font=LOGFONT(h=-13;name='FontAwesome');padding={left=12}};iconText='\uF014';notify=1;textPadding={left=30};z=6};
btnUpdate={cls="plus";text="更新订阅";left=726;top=512;right=848;bottom=548;align="left";bgcolor=11580047;db=1;dr=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-13;name='FontAwesome');padding={left=22}};iconText='\uF0C7';notify=1;textPadding={left=40};z=1};
chkAll={cls="plus";text="全部启用";left=11;top=489;right=118;bottom=520;align="left";bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-16;name='FontAwesome')};iconText='\uF0C8';notify=1;textPadding={left=19};z=3};
chkAutoRefreshSubscription={cls="plus";text="允许出站服务器异常自动刷新订阅";left=11;top=524;right=527;bottom=555;align="left";bgcolor=16777215;db=1;dl=1;dr=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-16;name='FontAwesome')};iconText='\uF0C8';notify=1;textPadding={left=19};z=2};
listview={cls="listview";left=10;top=36;right=883;bottom=477;db=1;dl=1;dr=1;dt=1;edge=1;fullRow=1;gridLines=1;z=4};
static={cls="static";text="双击上面单元格可直接编辑";left=678;top=486;right=848;bottom=507;align="right";color=10789024;db=1;dr=1;transparent=1;z=7}
)
/*}}*/

import style;
winform.chkAutoRefreshSubscription.skin(style.checkBox);  
winform.chkAll.skin(style.checkBox); 
winform.btnAdd.skin(style.plainButton);

winform.listview.insertColumn("",20) 
winform.listview.insertColumn("状态",50) 
winform.listview.insertColumn("备注",150) 
winform.listview.insertColumn("订阅地址",-1) 
winform.listview.checkbox = true;
winform.listview.gridLines = 0;

import win.ui.grid;
var grid = win.ui.grid(winform.listview);//创建数据视图
grid.readonlyColums = { [1] = true;[2] = true };
	
import config;
import inet.url;
var isOnCheckAll =0;
	
var reloadSubscriptions = function(){	
	isOnCheckAll =1;
	
	winform.chkAutoRefreshSubscription.checked = config.subscribe.autoRefreshSubscription; 
	config.subscribe.subscribeUrls.fields =  {"";"checked";"ps";"url";} 
	var cfgSubscribeUrls = config.subscribe.subscribeUrls;
	var chkAll = true;
	var checked = {}
	for(i=#cfgSubscribeUrls;1;-1){
		var sub = cfgSubscribeUrls[i] 
		if(sub.checked=="启用"){ 
			table.push(checked,i)
		}
		else {
			chkAll = false;
		}
	} 
	winform.chkAll.checked = chkAll;
	
	grid.setTable( cfgSubscribeUrls );
	winform.listview.checked = checked;	
	
	isOnCheckAll =0;
} 

winform.loadSubscriptionsOnce = function(){
	reloadSubscriptions();
	winform.loadSubscriptionsOnce = function(){};
}

subscribe("uiCommand.subscriptionNew",function(subItem,existIndex){
	if(existIndex){ 
		winform.listview.setItemText(subItem.url,existIndex,4); 	
		winform.listview.setChecked(existIndex,true);
		return;
	} 
		
	var item = winform.listview.addItem({"",subItem.checked,subItem.ps,subItem.url});
	winform.listview.setChecked(item,true);
} ) 

//编辑变更值会触发下面的事件
grid.onEditChanged = function(text,iItem,iSubItem){
	var name = config.subscribe.subscribeUrls.fields[iSubItem] 
	var sub = config.subscribe.subscribeUrls[iItem];

	if( name==="url" ){ 
		if( text[1]=='/'#) {
			text = "https://github.com"+text;
		}
		
		if(sub && sub.ps=="订阅源"){
			import inet.url;
			var tUrl = inet.url.split(text);
			if(tUrl&&#tUrl.host){
				sub.ps = tUrl.host;
				winform.listview.setItemText(sub.ps,iItem,3);
			} 
		}
	}

	if(sub){
		sub[name] = text;
		config.subscribe.save();
	}
	else {
		winform.msgErr("错误的订阅索引")
	}
} 

winform.btnUpdate.skin(style.primaryButton);
winform.btnUpdate.oncommand = function(id,event){

	winform.btnUpdate.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}
	config.subscribe.autoRefreshSubscription = winform.chkAutoRefreshSubscription.checked; 
	
	var outbondSubscribeUrlMap = {};
	var cfgSubscribeUrls = config.subscribe.subscribeUrls;
	if(#cfgSubscribeUrls == 0 ){
		..mainForm.msgErr("尚未导入订阅节点",1500);
		winform.btnUpdate.disabledText = null;
		return; 
	}
	for(i=#cfgSubscribeUrls;1;-1){
		var sub = cfgSubscribeUrls[i];
		// sub.checked等于启用时，outbondSubscribeUrlMap[sub.url]为true
		outbondSubscribeUrlMap[sub.url] = sub.checked=="启用";
	} 
		
	import xray.outbounds;
	if(!xray.outbounds.updateSubscription()){
		//winform.btnUpdate.disabledText = null;
		publish("uiCommand.restartxrayCore",outbounds) 
	}
	
	winform.btnUpdate.disabledText = null;
}

winform.chkAll.oncommand = function(id,event){
	isOnCheckAll = 1;
	var checked = winform.chkAll.checked
	var count =0
	for hItem in winform.listview.each(){
		//count +=1
		winform.listview.setChecked(hItem,checked)
		//win.msgbox(count,,,,100)
	}
	config.subscribe.save();
	isOnCheckAll =0;
}

winform.btnRemove.skin(style.plainButton)
winform.btnRemove.oncommand = function(id,event){
	
	var outboundRemoved = 0;
	var subscribeUrls = config.subscribe.subscribeUrls;
	var items = winform.listview.selected;
	if(!#items){
		return winform.msgWarn("未选中任何订阅项（ 请用鼠标单击订阅链接，不是指「勾选」 ）",2000)
	}
	
	winform.btnUpdate.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'}
	
	for(i=#items;1;-1){
		var url = subscribeUrls[items[i]].url;
		table.remove(subscribeUrls,items[i]);
		winform.listview.delItem(items[i]);
		
		var outbounds = ..config.proxy.outbounds : ..table.array();
		for(i=#outbounds;1;-1){
			var outbound = outbounds[i]  
			if(outbound.subscribeUrl===url){ 
				table.remove(outbounds,i);
				outboundRemoved++;
			}
		} 
	}
	
	config.proxy.save();
	config.subscribe.save();
	
	
	if(outboundRemoved){
		//winform.btnUpdate.disabledText = null;
		publish("uiCommand.restartxrayCore") 
	}
	else {
		win.delay(200);
	}
	winform.btnUpdate.disabledText = null;

}

winform.btnAdd.oncommand = function(id,event){
	import inet.url;
	import win.clip;
	
	var ps = "订阅源";
	var url = win.clip.read();
	url = url ? string.trim(url,'"\'\s\t\r\n ');
	if(url && url[1]=='/'#){
		url = "http://github.com" + url;
	}
	
	if(!inet.url.is(url)){
		url = "";
	}
	else {
		var tUrl = inet.url.split(url);
		if(tUrl && tUrl.host){
			ps = tUrl.host
		}
	} 
	
	config.subscribe.subscribeUrls[winform.listview.count+1] = {checked="启用";ps=ps;url=url}
	var item = winform.listview.addItem({
		"";"启用";ps;url
	})
	
	winform.listview.setChecked(item,true);
	grid.beginEdit(item,4);
}

winform.listview.onnotify = function(id,code,ptr){  
	if( code == 0xFFFFFF9B/*_LVN_ITEMCHANGED*/ ){ 
		var nmListView = winform.listview.getNotifyMessage(code,ptr);
		if( !nmListView )
			return;
			
		if( nmListView.uNewState & 0xF000/*_LVIS_STATEIMAGEMASK*/){
			var selIndex = nmListView.iItem;
			if( winform.listview.getChecked( selIndex ) ){
				config.subscribe.subscribeUrls[selIndex].checked = "启用";
				if (!isOnCheckAll ) config.subscribe.save();
				
				winform.listview.setItemText("启用",selIndex,2);
			}
			else {
				config.subscribe.subscribeUrls[selIndex].checked = "禁用";
				if (!isOnCheckAll ) config.subscribe.save();
				
				winform.listview.setItemText("禁用",selIndex,2); 
			} 
			
			var chkAll = true; 
			for(i=#config.subscribe.subscribeUrls;1;-1){
				var sub = config.subscribe.subscribeUrls[i] 
				if(sub.checked!="启用"){ 
					chkAll = false;
				}
			} 
				
			winform.chkAll.checked = chkAll;
		}  
	}
	elseif( code ==  0xFFFFFFF4/*_NM_CUSTOMDRAW*/ ) {
		var lvcd = winform.listview.getNotifyCustomDraw(code,ptr);
		if( lvcd.nmcd.dwDrawStage == 0x10001/*_CDDS_ITEMPREPAINT*/)
			return 0x20/*_CDRF_NOTIFYSUBITEMDRAW*/
		elseif( lvcd.nmcd.dwDrawStage == 1/*_CDDS_PREPAINT*/ ){
			return 0x20/*_CDRF_NOTIFYITEMDRAW*/;
		}
		elseif( lvcd.nmcd.dwDrawStage == ( 0x10001/*_CDDS_ITEMPREPAINT*/ | 0x20000/*_CDDS_SUBITEM*/) ){ 
			//注意这里 iSubItem 的索引自0开始( 其他函数通常自1开始 )
			lvcd.clrText = lvcd.nmcd.dwItemSpec % 2 ? 0x373737 : 0;
			lvcd.clrTextBk = lvcd.nmcd.dwItemSpec % 2 ? 0xFFFFFF : 0xF5F5F5;
			lvcd.update();
			
			return 0/*_CDRF_DODEFAULT*/
		}
	}
	elseif( code ==  0xFFFFFFFB/*_NM_RCLICK*/ )  {
		var x,y = win.getCursorPos();
		var nm = winform.listview.getNotifyMessage(code,ptr);
		if(!nm) return;
		
		var currentIdx = nm.iItem;
		// 没有选中item时的菜单
		if(!currentIdx){
			var popmenu = win.ui.popmenu(winform);
			
			popmenu.add('全部删除',function(id){ 
    		for(i=1; winform.listview.count)  winform.listview.setSelected(i,true);
				winform.btnRemove.oncommand();
			});
			popmenu.add('自剪贴板添加订阅',function(id){ 
    			winform.btnAdd.oncommand();
			});
			
			// 弹出菜单
			popmenu.popup(x,y,true);
			return;
		}
		
		// 有选中item时的菜单
		var selectedItems = winform.listview.selected;
		var popmenu = win.ui.popmenu(winform);
		if(#selectedItems==1){
			popmenu.add('更新当前订阅',function(id){
				var cfgSubscribeIndex = selectedItems[1]
				var currentSubscribeUrl = config.subscribe.subscribeUrls[cfgSubscribeIndex]['url'];
				config.subscribe.subscribeUrls[cfgSubscribeIndex].checked = "启用";
				config.subscribe.save();
				winform.listview.setChecked(currentIdx,true)
				winform.listview.setItemText("启用",currentIdx,2);
				xray.outbounds.updateSubscription({[currentSubscribeUrl]=true});
			});
			popmenu.add()
		}
		popmenu.add('删除选中',function(id){
			winform.btnRemove.oncommand();
		});
		popmenu.add('全部删除',function(id){ 
    	for(i=1; winform.listview.count)  winform.listview.setSelected(i,true);
			winform.btnRemove.oncommand();
		});
		popmenu.add('自剪贴板添加订阅',function(id){ 
    		winform.btnAdd.oncommand();
		});
		
		// 弹出菜单
		popmenu.popup(x,y,true);
	}
}

winform.show();
win.loopMessage();
return winform;